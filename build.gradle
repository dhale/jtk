apply plugin: 'java'

apply plugin: 'java-library-distribution'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
  mavenCentral()
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.8'
}

defaultTasks 'distZip'

description = 'Mines Java Toolkit'

sourceSets {
  main {
    output.resourcesDir = output.classesDir
  }
  test {
    output.resourcesDir = output.classesDir
  }
  demo {
    output.resourcesDir = output.classesDir
    compileClasspath += sourceSets.main.runtimeClasspath
  }
}

jar {
  baseName 'edu_mines_jtk'
  manifest {
    attributes 'Implementation-Title': project.description
  }
  from sourceSets.demo.output // include demos
  from sourceSets.test.output // include tests
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

javadoc {
  title = project.description
}

distributions {
  main {
    baseName = 'edu_mines_jtk_all'
    contents {
      from 'bin/'
    }
  }
}


dependencies {
  compile "org.jogamp.jogl:jogl-all-main:2.3.2",
          "org.jogamp.gluegen:gluegen-rt-main:2.3.2",
          "org.python:jython-standalone:2.5.3",
          "com.googlecode.netlib-java:netlib-java:0.9.3"

  testCompile "junit:junit:3.8.1"
}