description = 'Mines Java Toolkit Demos'

apply plugin: 'java'

// Demos do not (yet) have any unit tests.
test.enabled = false
compileTestJava.enabled = false
processTestResources.enabled = false
testClasses.enabled = false

// Demos require the Mines JTK core and some require Jython.
dependencies {
  compile project(':core')
  compile 'org.python:jython-standalone:2.7.0'
}

// A task to run a demo Java main class or Jython script.
task run(type: JavaExec) {
  description = 'Runs a demo Java main or Jython script. For example,\n' +
                '      $ gradle run -P demo=mosaic.PlotFrameDemo\n' +
                '      $ gradle run -P demo=mosaic/PlotFrameDemo.py'
  if (project.hasProperty('demo')) {
    enableAssertions = true
    classpath = sourceSets.main.runtimeClasspath
    //println "demo = "+demo // classpath may be useful for debugging
    //println "classpath = "; classpath.each { println it }
    if (demo.endsWith('.py')) {
      def pref = 'jtkdemo/'
      if (!demo.contains(pref))
        demo = pref + demo
      demo = 'src/main/jython/' + demo
      main = 'org.python.util.jython'
      jvmArgs '-Dpython.cachedir.skip=false',
              "-Dpython.cachedir=${buildDir}/jython-cache/"
      args demo
    } else {
      def pref = 'jtkdemo.'
      if (!demo.contains(pref))
        demo = pref + demo
      main = demo
    }
  }
}